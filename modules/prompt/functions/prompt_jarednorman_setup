#
# A simple theme based on the nicoulaj theme.
#
# Authors:
#   Jared Norman <hi@jarednorman.ca>
#   Julien Nicoulaud <julien.nicoulaud@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Features:
#   - Very much.
#
# Screenshots:
#   TODO
#

function prompt_jarednorman_precmd {
  vcs_info
}

function prompt_jarednorman_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_jarednorman_precmd

  # Customizable parameters.
  local max_path_chars=30
  local user_char='❯'
  local root_char='❯❯❯'
  local success_color='%F{2}'
  local failure_color='%F{1}'
  local vcs_info_color='%F{0}'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' unstagedstr 'dirty'
  zstyle ':vcs_info:*' stagedstr 'staged'
  zstyle ':vcs_info:*' actionformats "%S" "%r/%s/%b %u%c (%a)"
  zstyle ':vcs_info:*' formats "%S" "%r/%s/%b %u%c"
  zstyle ':vcs_info:*' nvcsformats "%~" ""

  # Define prompts.
  PROMPT="%(?.${success_color}.${failure_color})${SSH_TTY:+[%n@%m]}%${max_path_chars}<...<"'${vcs_info_msg_0_%%.}'"%<<%(!.${root_char}.${user_char})%f "
  RPROMPT="${vcs_info_color}"'${vcs_info_msg_1_}'"%f"
}

prompt_jarednorman_setup "$@"

